// Build a number linked to the date to use as a version.
def YEAR_WEEK    = new Date().format("yyww").toInteger()
def DAY          = new Date().format("u").toInteger()
def HOUR_MINUTE  = new Date().format("HHmm").toInteger()

// The build time
def BUILD_TIMESTAMP = 100000 * YEAR_WEEK + 10000 * DAY +  HOUR_MINUTE

// Semantic Versioning
def MAJOR = 1
def MINOR = 0
def PATCH = BUILD_TIMESTAMP
def NAME = "Pineapple"

// The build version
def BUILD_VERSION = "${NAME}-${MAJOR}.${MINOR}.${PATCH}"

// Top-level build file where you can add configuration options common to all sub-projects/modules.
// Apply the versions plugin
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.0"
        // Plugin to find the latest libraries version
        classpath "com.github.ben-manes:gradle-versions-plugin:0.34.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {

    ext.buildTimestamp = BUILD_TIMESTAMP
    ext.buildVersion = BUILD_VERSION

    // Use the temp directory to build temporals
    buildDir = "${System.env.TEMP}//gradle-build//${rootProject.name}//${project.name}"
    logger.lifecycle("Version(${BUILD_VERSION}) BuildDir(${buildDir})")

    repositories {
        google()
        jcenter()
        // External repository
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}